buildscript {
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
        }

        dependencies {
            classpath("com.android.tools.build:gradle:8.7.1")
            classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.21")
        }
    }
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def isNewArchitectureEnabled() {
    return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

if (isNewArchitectureEnabled()) {
    apply plugin: "com.facebook.react"
}
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace "dev.rems.reactnativeconfiginfo"
    buildToolsVersion safeExtGet("buildToolsVersion", "34.0.0")
    compileSdkVersion safeExtGet("compileSdkVersion", 34)

    defaultConfig {
        buildConfigField("boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString())
        minSdkVersion safeExtGet("minSdkVersion", 16)
        targetSdkVersion safeExtGet("targetSdkVersion", 34)
        versionCode 1
        versionName "1.0"
    }

    buildFeatures {
        buildConfig true
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

repositories {
    google()
    mavenCentral()
    maven {
        url("$rootDir/../node_modules/react-native/android")
    }
}

dependencies {
    implementation 'com.facebook.react:react-native:0.20.1'
}
